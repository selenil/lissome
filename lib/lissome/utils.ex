defmodule Lissome.Utils do
  @doc """
  Turns a module name into a valid Gleam module name.

  Gleam uses `@` to separate paths in module names. For example, a module
  in the directory `src/pages/home` would be compiled as `pages@home`. We process the path to turn it into the correct erlang module name that we can call in Elixir.

  ## Examples:

    iex> Lissome.Utils.format_module_name("nested/nested/nested/mod")
    :nested@nested@nested@mod

    iex> Lissome.Utils.format_module_name("home")
    :home
  """
  def format_module_name(module_path) do
    module_path
    |> Path.split()
    |> Enum.join("@")
    |> String.to_atom()
  end

  @doc """
  Extracts an Erlang record generated by Gleam and creates the corresponding Elixir tuple with the values passed in.

  By default, this function will look for the `.hrl` file in the `_{gleam_app_name}/include` directory, where `gleam_app_name` is the value of the `:gleam_dir` config in `lissome`, unless a fourth argument is provided, specifying the path to the `.hrl` file.

  Returns a tuple with its first element being the record name and the rest of the elements being the values in the order Erlang expects them. If any of the record keys are not present in the values map, that field will be set to `nil`.

  ## Examples

      iex> flags = %{count: 5, name: "John"}
      # assuming you have a `Flags` type in your Gleam code that expects first
      # the name and then the count
      iex> extract_and_create_record("my_gleam_module", :flags, flags)
      {:flags, "John", 5}

  """
  def extract_and_create_record(module_name, flags_type, values, hrl_file_path \\ nil) do
    hrl_file_path =
      hrl_file_path || build_hrl_file_path(module_name, flags_type)

    flags_type
    |> Record.extract(from: hrl_file_path)
    |> Enum.map(fn {key, _} -> Map.get(values, key) end)
    |> List.to_tuple()
    |> Tuple.insert_at(0, flags_type)
  end

  defp build_hrl_file_path(module_name, flags_type) do
    gleam_dir = get_gleam_app_from_config()
    flags_type_string = flags_type |> Atom.to_string() |> String.capitalize()

    Path.join([
      Mix.Project.build_path(),
      "lib",
      "_#{gleam_dir}",
      "include",
      "#{module_name}_#{flags_type_string}.hrl"
    ])
  end

  def json(data), do: Elixir.JSON.encode!(data)

  def get_gleam_app_from_config do
    Application.get_env(:lissome, :gleam_app, nil)
  end

  # replace this with a call to the gleam export package-info
  # command when it's added to gleam
  def extract_gleam_app_name(gleam_dir) do
    content =
      gleam_dir
      |> Path.join("gleam.toml")
      |> File.read!()

    Regex.run(~r/name = "(.*)"/, content) |> List.last()
  end
end
