defmodule Lissome.GleamBuilder do
  @moduledoc """
  Wrapper around the Gleam build tool.

  This module provides functionality to build Gleam source files to either JavaScript or Erlang targets using the Gleam CLI.
  """

  @gleam_pattern "**/*.gleam"
  @default_gleam_dir Application.compile_env(:lissome, :gleam_dir, "assets/lustre_app")

  defguard is_valid_target(target) when target in ["javascript", "erlang"]

  @doc """
  Builds Gleam source files to the specified target using the `gleam build` command.

  The path to the Gleam source files can be specified via the `:gleam_dir` option. If not provided, it will use the value of `:gleam_dir` from the `lissome` application config with a fallback to `"assets/lustre_app"` if the config is not set.

  When `:compile_package` is set to `true`, the `gleam compile-package` command will be used instead of `gleam build`.

  If the target is `:erlang`, the compiled modules will be automatically loaded.

  Returns `:ok`.

  ## Examples

      iex> Lissome.GleamBuilder.build_gleam(:erlang)

      iex> Lissome.GleamBuilder.build_gleam(:javascript)

      iex> Lissome.GleamBuilder.build_gleam(:javascript, gleam_dir: "assets/my_gleam_app", compile_package: true)

  """
  def build_gleam(target, opts \\ [])

  def build_gleam(targets, opts) when is_list(targets) and is_list(opts) do
    Enum.each(targets, &build_gleam(&1, opts))
  end

  def build_gleam(target, opts) when is_list(opts) do
    gleam_dir = opts[:gleam_dir] || @default_gleam_dir
    gleam_src = Path.join(gleam_dir, "src")

    gleam_files =
      gleam_src
      |> Path.join(@gleam_pattern)
      |> Path.wildcard()

    gleam? =
      File.exists?(gleam_src) and not Enum.empty?(gleam_files)

    if gleam?, do: build(target, gleam_dir, Keyword.drop(opts, [:gleam_dir]))

    :ok
  end

  defp build(target, gleam_dir, opts)
       when is_atom(target) do
    target = Atom.to_string(target)
    build(target, gleam_dir, opts)
  end

  defp build(target, gleam_dir, opts)
       when is_binary(target)
       when is_valid_target(target) do
    compile_package? = Keyword.get(opts, :compile_package, false)
    load_beam_modules? = Keyword.get(opts, :load_beam_modules, true)

    gleam_app =
      ((compile_package? || load_beam_modules?) &&
         extract_gleam_app_name(gleam_dir)) ||
        nil

    args =
      if compile_package?,
        do: compile_package_args(target, gleam_app),
        else: ["build", "--target", target]

    {_, exit_code} = cmd("gleam", args, cd: gleam_dir)

    if load_beam_modules? and exit_code == 0 and target == "erlang" do
      erlang_outdir =
        Mix.Project.build_path()
        |> Path.join(opts[:erlang_outdir] || "lib/_#{gleam_app}/ebin")
        |> String.to_charlist()

      compile_and_load_erlang_modules(gleam_dir, gleam_app, erlang_outdir)
    end
  end

  defp compile_package_args(target, gleam_app) do
    build = "build/dev/#{target}"
    out = Path.join(build, gleam_app)

    target_specific_args =
      case target do
        "erlang" -> ["--no-beam"]
        "javascript" -> ["--javascript-prelude", build]
      end

    [
      "compile-package",
      "--target",
      target,
      "--package",
      ".",
      "--out",
      out,
      "--lib",
      build
    ] ++ target_specific_args
  end

  defp cmd(command, args, opts) do
    opts =
      Keyword.merge(
        opts,
        into: IO.stream(:stdio, :line),
        stderr_to_stdout: true
      )

    System.cmd(command, args, opts)
  end

  defp compile_and_load_erlang_modules(gleam_dir, gleam_app, outdir) do
    build_path = "build/dev/erlang/#{gleam_app}/_gleam_artefacts/"

    File.mkdir_p!(outdir)

    gleam_dir
    |> Path.join([build_path, "**/*.erl"])
    |> Path.wildcard()
    |> Enum.each(fn file ->
      # skip escript generated by gleam
      if String.ends_with?(file, "@@compile.erl"),
        do: :ok,
        else: compile_erl_file(file, outdir)
    end)

    Code.prepend_path(outdir, cache: true)

    :ok
  end

  defp compile_erl_file(file, outdir) do
    file = String.replace(file, ".erl", "") |> String.to_charlist()

    opts = [
      :report_errors,
      :report_warnings,
      {:outdir, outdir}
    ]

    :compile.file(file, opts)
  end

  # replace this with a call to the gleam export package-info
  # command when it's added to gleam
  defp extract_gleam_app_name(gleam_dir) do
    content =
      gleam_dir
      |> Path.join("gleam.toml")
      |> File.read!()

    Regex.run(~r/name = "(.*)"/, content) |> List.last()
  end
end
